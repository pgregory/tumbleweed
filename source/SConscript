Import('env', 'install_dir')

common_source = Split("""
  filein.c
  interp.c
  lex.c
  memory.c
  names.c
  parser.c
  primitive.c
  sysprimitive.c
  unixio.c
""")

sockets_source = Split("""
  sockets/sockets.c
""")

env.Append(LIBS = env['TW_LIBS'])

main_env = env.Clone();
main_env.VariantDir('main', '.')
main_env.Append(CPPDEFINES = ['TW_NO_COMPILER'])

initial_env = env.Clone();
initial_env.VariantDir('init', '.')

tw_source = [ 'main/st.c' ]
tw_source += [ 'main/%s' % x for x in common_source ] 
tw_source += [ 'main/%s' % x for x in sockets_source ] 
tw = main_env.Program('tw', tw_source)

initial_source = [ 'init/initial.c' ]
initial_source += [ 'init/%s' % x for x in common_source ] 
initial_source += [ 'init/%s' % x for x in sockets_source ] 
initial = initial_env.Program('initial', initial_source)

main_env.Install(install_dir, tw)

Export('initial')

# ex: set filetype=python tabstop=2 shiftwidth=2 expandtab: 
